#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЭтотОбъект.Параметры.Свойство("КалендарьСотрудника") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	КалендарьСотрудника = ЭтотОбъект.Параметры.КалендарьСотрудника;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТекстУведомленияПриИзменении(Элемент)
	Элементы.ТекстУведомления.АвтоОтметкаНезаполненного = ПустаяСтрока(СокрЛП(ЭтотОбъект.ТекстУведомления));
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)
	
	Если ПустаяСтрока(СокрЛП(ЭтотОбъект.ТекстУведомления)) Тогда
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Перед сохранением необходимо заполнить текст уведомления!'"),,
													"ТекстУведомления", "ТекстУведомления");
		Возврат;
		
	КонецЕсли;
	
	Если ПустаяСтрока(СокрЛП(ЭтотОбъект.ТипУведомления)) Тогда
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Перед сохранением необходимо заполнить тип уведомления!'"),,
													"ТипУведомления", "ТипУведомления");
		Возврат;
		
	КонецЕсли;
	
	ЗаписатьДанныеОИзмененииТекстаУведомления(ЭтотОбъект.КалендарьСотрудника, ЭтотОбъект.ТекстУведомления, ЭтотОбъект.ТипУведомления);
	Закрыть();
	
КонецПроцедуры  

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Записывает значения характеристик в регистр
//
// Параметры:
// КалендарьСотрудника - СправочникСсылка.КалендариСотрудников
// ТекстУведомления - Строка
// ТипУведомления - Строка
//
&НаСервереБезКонтекста
Процедура ЗаписатьДанныеОИзмененииТекстаУведомления(Знач КалендарьСотрудника, Знач ТекстУведомления, Знач ТипУведомления)

	МенеджерИзменений = РегистрыСведений.Пр_НастройкиУведомленийКалендарей.СоздатьМенеджерЗаписи();
	МенеджерИзменений.КалендарьСотрудника = КалендарьСотрудника;
	МенеджерИзменений.Прочитать();  
	МенеджерИзменений.КалендарьСотрудника = КалендарьСотрудника;
	Если ТипУведомления = "Анализ" Тогда
		МенеджерИзменений.ТекстУведомленийАнализ = ТекстУведомления; 
	ИначеЕсли ТипУведомления = "ВРаботе" Тогда
		МенеджерИзменений.ТекстУведомленийВРазработке = ТекстУведомления;	
	КонецЕсли;
	МенеджерИзменений.Записать(Истина);  
	
КонецПроцедуры

#КонецОбласти
