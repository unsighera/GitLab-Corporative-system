#Область ОбработчикиСобытийФормы

&НаСервере
Процедура Пр_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)            
	
	Пр_МодификацияФормСервер.ПриСозданииНаСервере(ЭтотОбъект, СтандартнаяОбработка, Отказ);  
	УстановитьНастройкиУведомленийТекущейДоски(ЭтотОбъект.ТекущаяДоска);
	
КонецПроцедуры

&НаКлиенте
Процедура Пр_ПриОткрытииПосле(Отказ)       
	УстановитьНастройкуУведомлений(Неопределено);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура Пр_ПереключательПриИзмененииПосле(Элемент)

	Если ВидыКонтактЦентра = 5 Тогда 
		ОбновитьЛимитыКолонокКалендаря();
	КонецЕсли;	 
	
	УстановитьНастройкиУведомленийТекущейДоски(ЭтотОбъект.ТекущаяДоска);
    УстановитьНастройкуУведомлений(Неопределено);
	
КонецПроцедуры     

&НаКлиенте
&ИзменениеИКонтроль("Подключаемый_Перетаскивание")
Процедура Пр_Подключаемый_Перетаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)

	СтандартнаяОбработка = Ложь;
	Индекс = ИндексЭлемента(Элемент.Имя);
	КолонкаКалендаря = КолонкиТекущейДоски[Индекс].Ссылка;

	Если ПараметрыПеретаскивания.Значение = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ПараметрыПеретаскивания.Значение.Вставить("Колонка",КолонкаКалендаря);
	
	#Вставка
	ЛимитыПоКолонкам = ПолучитьЛимитыКолонокТекущейДоски(КолонкиТекущейДоски);
	ЭлементСоответсвия = ЛимитыПоКолонкам.Получить(КолонкаКалендаря);
	Если ЭлементСоответсвия <> Неопределено Тогда
		Если ЭлементСоответсвия.МаксимальноеКоличество <> 0
			 И ЭлементСоответсвия.ТекущееКоличество = ЭлементСоответсвия.МаксимальноеКоличество
		Тогда 
			Шаблон = НСтр("ru = 'По колонке %1 превышен VIP лимит!'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Шаблон, КолонкаКалендаря);
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения); 
			Возврат;
		КонецЕсли;		
	КонецЕсли;
	#КонецВставки
	
	ОбновитьКолонкуЗаписиКалендаря(ПараметрыПеретаскивания.Значение, Элемент.Имя);

	Для Каждого СписокКолонки Из КолонкиТекущейДоски Цикл
		Индекс = КолонкиТекущейДоски.Индекс(СписокКолонки);
		Элементы["СписокДел_"+Индекс].ВыделенныеСтроки.Очистить();
	КонецЦикла;

	ТекущийЭлемент = Элемент;
	Элемент.ТекущаяСтрока = Новый КлючСтрокиДинамическогоСписка("ЗаписьКалендаря, Ссылка",
	ПараметрыПеретаскивания.Значение.ЗаписьКалендаря,
	ПараметрыПеретаскивания.Значение.Значение);

	ПоказатьОповещениеПользователя(НСтр("ru='Изменение:'"),
	ПолучитьНавигационнуюСсылку(ПараметрыПеретаскивания.Значение.Значение),
	СтрШаблон(НСтр("ru='%1'"),ПараметрыПеретаскивания.Значение.Значение),
	БиблиотекаКартинок.Информация32); 
	
	#Вставка
	ОбновитьЛимитыКолонокКалендаря(); 
	Пр_ОбменGitLabКлиент.ОтправитьIssue(ПараметрыПеретаскивания.Значение.Значение);
	#КонецВставки
	
КонецПроцедуры

&НаКлиенте
&После("Подключаемый_ЗаголовокДоскиНажатие")
Процедура Пр_Подключаемый_ЗаголовокДоскиНажатие(Элемент)

	ОбновитьЛимитыКолонокКалендаря();
	УстановитьНастройкиУведомленийТекущейДоски(ЭтотОбъект.ТекущаяДоска);
	УстановитьНастройкуУведомлений(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНастройкуУведомлений(Элемент)

	Элементы.Пр_МаксимальныйСрокАнализаДни.Видимость = ЭтотОбъект.Пр_УведомлениеОДлительномАнализе;
	Элементы.Пр_ПользовательДляУведомленияАнализ.Видимость = ЭтотОбъект.Пр_УведомлениеОДлительномАнализе;
	
	Элементы.Пр_МаксимальныйСрокРазработкиДни.Видимость = ЭтотОбъект.Пр_УведомлениеОДлительнойРазработке;
	Элементы.Пр_ПользовательДляУведомленияРазработка.Видимость = ЭтотОбъект.Пр_УведомлениеОДлительнойРазработке;
	
	Элементы.Пр_ИзменитьТекстУведомления.Видимость = ?(ЭтотОбъект.Пр_УведомлениеОДлительномАнализе Или ЭтотОбъект.Пр_УведомлениеОДлительнойРазработке, Истина, Ложь);
	
	Если Элемент <> Неопределено Тогда     
		
		ПоляНастроек = "КалендарьСотрудника, УведомлениеОДлительномАнализе,
		|УведомлениеОДлительнойРазработке, МаксимальныйСрокАнализаДни, МаксимальныйСрокРазработкиДни,
		|ПользовательУведомленийАнализ, ПользовательУведомленийРазработка";
		
		СтруктураНастроек = Новый Структура(ПоляНастроек,
											ЭтотОбъект.ТекущаяДоска,
											ЭтотОбъект.Пр_УведомлениеОДлительномАнализе,
											ЭтотОбъект.Пр_УведомлениеОДлительнойРазработке,
											ЭтотОбъект.Пр_МаксимальныйСрокАнализаДни,
											ЭтотОбъект.Пр_МаксимальныйСрокРазработкиДни,
											ЭтотОбъект.Пр_ПользовательДляУведомленияАнализ,
											ЭтотОбъект.Пр_ПользовательДляУведомленияРазработка);
		УстановитьНастройкуПоказателя(СтруктураНастроек);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Пр_ИзменитьТекстУведомления(Команда)
	
	ПараметрыОткрытия = Новый Структура("КалендарьСотрудника", ТекущаяДоска);
	ОткрытьФорму("Обработка.КонтактЦентр.Форма.Пр_ФормаРедактированияТекстаУведомлений",
				 ПараметрыОткрытия,
				 ЭтотОбъект,,,,,
				 РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
				 
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Устанавливает в заголовое колонок установленные лимиты
//
&НаКлиенте
Процедура ОбновитьЛимитыКолонокКалендаря()
	
	ЛимитыПоКолонкам = ПолучитьЛимитыКолонокТекущейДоски(КолонкиТекущейДоски);
	
	Для Каждого Колонка Из КолонкиТекущейДоски Цикл
		
		ЭлементСоответствия = ЛимитыПоКолонкам.Получить(Колонка.Ссылка);
		Если ЭлементСоответствия = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЭлементСоответствия.МаксимальноеКоличество = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Шаблон = "ДекорацияЗаголовокЗадачи_[ИндексКолонки]";
		ШаблонЛимита = "[ИмяКолонки] ([ТекущееКоличество]/[МаксимальноеКоличество])";
		ЗаголовокКолонкиИмя = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(Шаблон, Новый Структура("ИндексКолонки", КолонкиТекущейДоски.Индекс(Колонка)));
		Элементы[ЗаголовокКолонкиИмя].Заголовок = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонЛимита,
																										   Новый Структура("ИмяКолонки, ТекущееКоличество, МаксимальноеКоличество",
																										   Строка(Колонка.Ссылка),
																										   ЭлементСоответствия.ТекущееКоличество,
																										   ЭлементСоответствия.МаксимальноеКоличество));
		
	КонецЦикла;

	
КонецПроцедуры

// Получает лимиты по колонкам текущей доски
//
// Параметры:
// КолонкиТекущейДоски - ТаблицаЗначений
//  * Ссылка - СправочникСсылка.КолонкиКалендарейСотрудников 
//	
// Возвращаемое значение:
// Соответствие
//  * Ключ - СправочникСсылка.КолонкиКалендарейСотрудников
//	* Значение - Структура
//		* МаксимальноеКоличество - Число
//		* ТекущееКоличество - Число
//
&НаСервереБезКонтекста
Функция ПолучитьЛимитыКолонокТекущейДоски(Знач КолонкиТекущейДоски)
	
	ЛимитыПоКолонкам = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	К.Ссылка КАК Колонка
		|ПОМЕСТИТЬ ВТ_Колонки
		|ИЗ 
		|	&КолонкиТекущейДоски КАК К
		|ИНДЕКСИРОВАТЬ ПО
		|	Колонка
		|;
		|
		|////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	К.Колонка КАК Колонка,
		|	Л.Лимит КАК МаксимальноеКоличество
		|ПОМЕСТИТЬ ВТ_МаксимальныйЛимит
		|ИЗ 
		|	ВТ_Колонки КАК К
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Пр_ЛимитыПоКолонкам КАК Л
		|		ПО К.Колонка = Л.Колонка
		|ИНДЕКСИРОВАТЬ ПО
		|	Колонка
		|;
		|
		|////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_Колонки
		|;
		|
		|////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	З.КолонкаКалендаря КАК Колонка,
		|	КОЛИЧЕСТВО(З.Ссылка) КАК ТекущееКоличество
		|ПОМЕСТИТЬ ВТ_ТекущийЛимит
		|ИЗ
		|	Справочник.ЗаписиКалендаряСотрудника КАК З
		|ГДЕ
		|	З.КолонкаКалендаря В (ВЫБРАТЬ
		|						      Т.Колонка
		|						  ИЗ
		|						      ВТ_МаксимальныйЛимит КАК Т)
		|	И НЕ З.ПометкаУдаления 
		|
		|СГРУППИРОВАТЬ ПО
		|	З.КолонкаКалендаря 
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Колонка
		|;   
		|
		|/////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	М.Колонка КАК Колонка,
		|	ЕСТЬNULL(М.МаксимальноеКоличество, 0) КАК МаксимальноеКоличество,
		|	ЕСТЬNULL(Т.ТекущееКоличество, 0) КАК ТекущееКоличество
		|ИЗ
		|	ВТ_МаксимальныйЛимит КАК М
		|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТекущийЛимит КАК Т
		|		ПО М.Колонка = Т.Колонка
		|;
		|
		|//////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_ТекущийЛимит
		|;
		|
		|//////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_МаксимальныйЛимит";
	
	Запрос.УстановитьПараметр("КолонкиТекущейДоски", КолонкиТекущейДоски.Выгрузить());
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЛимитыПоКолонкам.Вставить(Выборка.Колонка, Новый Структура("ТекущееКоличество, МаксимальноеКоличество",
																   Выборка.ТекущееКоличество,
																   Выборка.МаксимальноеКоличество));	
	КонецЦикла;  
															   
	Возврат ЛимитыПоКолонкам;
		
КонецФункции	

// Устанавливает значения настроек уведомлений при изменении доски
//
// Параметры:
// КалендарьСотрудника - СправочникСсылка.КалендариСотрудников
//
&НаСервере
Процедура УстановитьНастройкиУведомленийТекущейДоски(Знач КалендарьСотрудника)
	
	Если Не ЗначениеЗаполнено(КалендарьСотрудника)
		 Или КалендарьСотрудника = Справочники.КалендариСотрудников.ПустаяСсылка() Тогда
		Возврат;
	КонецЕсли;
	
	ПоляНастроек = "Пр_УведомлениеОДлительномАнализе,Пр_УведомлениеОДлительнойРазработке,Пр_МаксимальныйСрокАнализаДни,
		   		   |Пр_МаксимальныйСрокРазработкиДни,Пр_ПользовательДляУведомленияАнализ,Пр_ПользовательДляУведомленияРазработка";
	
	МассивПолейНастроек = СтрРазделить(ПоляНастроек, ",", Ложь);
	Для Каждого Настройка Из МассивПолейНастроек Цикл
		
		ЭтотОбъект[СокрЛП(Настройка)] = Неопределено;
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	Пр_НастройкиУведомленийКалендарей.УведомлениеОДлительномАнализе КАК Пр_УведомлениеОДлительномАнализе,
		|	Пр_НастройкиУведомленийКалендарей.УведомлениеОДлительнойРазработке КАК Пр_УведомлениеОДлительнойРазработке,
		|	Пр_НастройкиУведомленийКалендарей.МаксимальныйСрокАнализаДни КАК Пр_МаксимальныйСрокАнализаДни,
		|	Пр_НастройкиУведомленийКалендарей.МаксимальныйСрокРазработкиДни КАК Пр_МаксимальныйСрокРазработкиДни,
		|	Пр_НастройкиУведомленийКалендарей.ПользовательУведомленийАнализ КАК Пр_ПользовательДляУведомленияАнализ,
		|	Пр_НастройкиУведомленийКалендарей.ПользовательУведомленийРазработка КАК Пр_ПользовательДляУведомленияРазработка
		|ИЗ
		|	РегистрСведений.Пр_НастройкиУведомленийКалендарей КАК Пр_НастройкиУведомленийКалендарей
		|ГДЕ
		|	Пр_НастройкиУведомленийКалендарей.КалендарьСотрудника = &КалендарьСотрудника";
	
	Запрос.УстановитьПараметр("КалендарьСотрудника", КалендарьСотрудника);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);	
	КонецЕсли;
	
КонецПроцедуры

// Записывает настройки календаря в РС
//
// Параметры:  
// СтруктураНастроек - Структура
// 	* КалендарьСотрудника - СправочникСсылка.КалендариСотрудников
// 	* УведомлениеОДлительномАнализе - Булево
// 	* УведомлениеОДлительнойРазработке - Булево
// 	* МаксимальныйСрокАнализаДни - Число (3,0) Неотрицательное
// 	* МаксимальныйСрокРазработкиДни - Число (3,0) Неотрицательное
// 	* ПользовательУведомленийАнализ - СправочникСсылка.Пользователи
// 	* ПользовательУведомленийРазработка - СправочникСсылка.Пользователи
//
&НаСервереБезКонтекста
Процедура УстановитьНастройкуПоказателя(Знач СтруктураНастроек)
	
	МенеджерИзменений = РегистрыСведений.Пр_НастройкиУведомленийКалендарей.СоздатьМенеджерЗаписи();
	МенеджерИзменений.КалендарьСотрудника = СтруктураНастроек.КалендарьСотрудника;
	МенеджерИзменений.Прочитать();
	ЗаполнитьЗначенияСвойств(МенеджерИзменений, СтруктураНастроек);
	МенеджерИзменений.Записать(Истина); 
	
КонецПроцедуры

&НаСервере
&ИзменениеИКонтроль("ОбновитьКолонкуЗаписиКалендаря")
Процедура Пр_ОбновитьКолонкуЗаписиКалендаря(ПараметрыПеретаскивания, ИмяСписка)

	Попытка	
		Если ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("ДокументСсылка.ЗадачаСотрудника") Тогда
			ЗаписьОбъект = ПараметрыПеретаскивания.Значение.ПолучитьОбъект();
		Иначе
			ЗаписьОбъект = ПараметрыПеретаскивания.ЗаписьКалендаря.ПолучитьОбъект();
		КонецЕсли;
		НачатьТранзакцию();
		ЗаписьОбъект.Заблокировать();
		ЗаписьОбъект.КолонкаКалендаря = ПараметрыПеретаскивания.Колонка; 
		
		#Вставка                                                         
		Если ТипЗнч(ЗаписьОбъект) = Тип("СправочникОбъект.ЗаписиКалендаряСотрудника") Тогда
			ЗаписьОбъект.Пр_ДатаПеремещения = ТекущаяДатаСеанса();	
		КонецЕсли;
		#КонецВставки
		
		ЗаписьОбъект.Записать();
		ЗаписьОбъект.Разблокировать();

		Справочники.КолонкиКалендарейСотрудников.ПриИзмененииКолонкиЗаписи(ЗаписьОбъект.Ссылка, 
		ПараметрыПеретаскивания.Колонка);
		ЗафиксироватьТранзакцию();

		Элементы[ИмяСписка].Обновить();
		Элементы[ПараметрыПеретаскивания.СписокДляОбновления].Обновить();

		ТекущийЭлемент = Элементы[ИмяСписка];
		Элементы[ИмяСписка].ТекущаяСтрока = ПараметрыПеретаскивания.Значение.Значение;
	Исключение

	КонецПопытки

КонецПроцедуры

#КонецОбласти
