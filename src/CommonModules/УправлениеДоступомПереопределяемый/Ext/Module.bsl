#Область СлужебныеПроцедурыИФункции

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////// 
// 							                                                                                                                             //
//							ПАМЯТКА ДЛЯ МЕНТАЛЬНО ПАДШИХ ОТ ПЛОХОГО ПОВЕДЕНИЯ RLS БСП                  													 //
//                                                                                                                                                       //
// В типовых базах с подключенным ограничением на уровне записей дела обстоят по другому.    															 //
// В чем собственно сама загвоздка:                                                                                                                      //
// Некоторые типовые базы почему-то НЕ МОГУТ самстоятельно обновлять используемые виды и                                                                 // 
// Профили групп доступа. При этом при правильной настройке по руководству ИТС, настроить                                                                //
// Права по нему удасться только В ШАБЛОННОЙ БАЗЕ БСП. Так как не вся необходимая информация                                                             //
// Указана в руководстве. Итак, для добавления нового вида доступа, необходимо:				                                                             //
// 1. Включить свой справочник в определяемые типы:                                                                                                      //
//     а) ВладелецЗначенийКлючейДоступа                                                                                                                  //
//     б) ЗначениеДоступа                                                                                                                                //
//     в) ЗначениеДоступаОбъект                                                                                                                          //
//     г) ВладелецКлючейДоступаОбъект                                                                                                                    //
//     д) ПолеРегистраКлючейДоступаКРегистрам                                                                                                            //
//                                                                                                                                                       //
//    Эти определяемые типы выявлены в ходе мучительной отладки.                                                                                         //
//                                                                                                                                                       //
// 2. Необходимо добавить инициализацию вида доступа в процедуру ПриЗаполненииВидовДоступа                                                               //
//	  см. УправлениеДоступомПереопределяемый.ПриЗаполненииВидовДоступа                                                                                   //
//                                                                                                                                                       //
// 3. Необходимо добавить код инициализации в процедуру ПриЗаполненииИспользованияВидаДоступа                                                            //
//	  см. УправлениеДоступомПереопределяемый.ПриЗаполненииИспользованияВидаДоступа                                                                       //
//	                                                                                                                                                     // 
// 4. Необходимо добавить места использования добавленного вида доступа в процедуру                                                                      //
//	  ПриЗаполненииВидовОграниченийПравОбъектовМетаданных                                                                                                //
//	  см. УправлениеДоступомПереопределяемый.ПриЗаполненииВидовОграниченийПравОбъектовМетаданных                                                         //
//                                                                                                                                                       //
// На этом вид доступа будет добавлен, НО как и писалось выше, некоторые типовые базы и                                                                  //
// Инструменты разработки поставки БСП не способны обновить со 100% вероятностью все ключи доступа                                                       //
// Т.к. он сверяет Hash и он актуален и это может помешать при разработке видов ограничений доступа                                                      //
//                                                                                                                                                       //
// Для ручного обновления ключей доступа необходимо во внешней обработке вызвать следующий код                                                           //
//                                                                                                                                                       //
// РегистрыСведений.ИспользуемыеВидыДоступа.ОбновитьДанныеРегистра(Истина, Истина);                                                                      //
// ЕстьИзменения = Неопределено;                                                                                                                         //
// РегистрыСведений.ПраваРолей.ОбновитьДанныеРегистра(ЕстьИзменения);                                                                                    //
// РегистрыСведений.ТаблицыГруппДоступа.ОбновитьДанныеРегистра();                                                                                        //
// Dызов внутри следующей процедуры не срабатывает, т.к. версии конфы совпадает.                                                                         //
// УправлениеДоступомСлужебный.ОбновитьВспомогательныеДанныеПоИзменениямКонфигурации()                                                                   //
//                                                                                                                                                       //
// Также следует помнить и читать документацию 1С по релизам платформ, и понимать базовые                                                                //
// Приницпы работы с расширениями, т.к., в случае разработке прав                                                                                        //
// На версии платформы не менее чем 8.3.20, работа с определяемыми типами невозможна                                                                     //
// И остатется лишь снимать с полной поддержки конфигурацию и вносить в изменения в основной.                                                            //
// В случае работы с раширением на более новой версии необходимо контроллировать вид                                                                     //
// Применения в расширении (см. пиктограмму слева от чек бокса, должен быть оранжевый пазл                                                               //
//                                                                                                                                                       //
// Основные нюансы закончились                                                                                                                           //
//                                                                                                                                                       //
// Добавление нового документа в систему RLS:                                                                                                            // 
//                                                                                                                                                       //
// 1. Документ нужно включить также в определяемые типы:                                                                                                 //
//     а) ВладелецЗначенийКлючейДоступа                                                                                                                  //
//	   б) ВладелецЗначенийКлючейДоступаДокумент                                                                                                          //
//	   в) ЗначениеДоступа                                                                                                                                //
//	   г) ЗначениеДоступаОбъект                                                                                                                          //
//	   д) ПолеРегистраКлючейДоступаКРегистрам                                                                                                            //
//                                                                                                                                                       //
// Состав определяемых типов также был подобран исходя из мучительной отладки                                                                            //
//                                                                                                                                                       //
// 2. Документ нужно включить в следующие процедуры УправлениеДоступомПереопределяемый:                                                                  //
//	   а) ПриЗаполненииСписковСОграничениемДоступа                                                                                                       //
//	   б) ПриЗаполненииВидовОграниченийПравОбъектовМетаданных                                                                                            //
//                                                                                                                                                       //
// 3. Также необходимо добавить атомарные роли на Чтение и ДобавлениеИзменение с текстами                                                                //
// 	  Шаблонов описанных в типовой роли ИзменениеУчастниковГруппДоступа (там находятся                                                                   //
// 	  Актуальные шаблоны для текущего релиза конфигурации), из ИНТЕРНЕТА БРАТЬ НЕЛЬЗЯ                                                                    //
//                                                                                                                                                       //
// 4. Текст ограничения доступа будет выглядеть следующим образом                                                                                        //
//                                                                                                                                                       //
// 	  #Если &ОграничениеДоступаНаУровнеЗаписейУниверсально #Тогда                                                                                        //
//        #ДляОбъекта("")                                                                                                                                //
//	  #Иначе                                                                                                                                             //
//        #ПоЗначениямРасширенный("Документ.Пр_ОтчетЗаДень", "", "",                                                                                     //
//            "ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Уровень3Сотрудники ПО Т.Сотрудник = Уровень3Сотрудники.Ссылка                                  //
//    		   ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Уровень2Сотрудники ПО Уровень3Сотрудники.ГоловнойСотрудник = Уровень2Сотрудники.Ссылка         //
//    		   ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Уровень1Сотрудники ПО Уровень2Сотрудники.ГоловнойСотрудник = Уровень1Сотрудники.Ссылка",       //
//    		   "",                                                                                                                                       //
//    		   "СтруктурнаяЕдиница", "Т.Ссылка", "И(",                                                                                                   //
//    		   "Сотрудник", "Т.Ссылка", "ИЛИ",                                                                                                           //
//    		   "Сотрудник", "Уровень2Сотрудники.Ссылка", "ИЛИ",                                                                                          //
//    		   "Сотрудник", "Уровень1Сотрудники.Ссылка", ")",                                                                                            //
//    		   "", "", "", "", "", "",                                                                                                                   //
//    		   "","","", "","","", "","","", "","","", "","","", "","","", "","","", "","","", "","","", "","","")                                       //
//   #КонецЕсли                                                                                                                                          //
//                                                                                                                                                       //
//	 Верхнее условие проверяет, что включен производительный режим RLS, и принимает параметр                                                             //
//	 Поле объекта по которому будем резать, чаще всего используется пустая строка                                                                        //
//	                                                                                                                                                     //
//   Нижняя конструкция обозначает использование стандартного режима работы RLS                                                                          //
//	 Крайне рекомендуется при отсутствии необходимости в соединениях таблиц использовать шаблон #ПоЗначениям                                             //
//	                                                                                                                                                     //
//	 Если стоит необходимость использования соединений или конструкции ИЛИ, то использовать текущий вариант                                              //
//	                                                                                                                                                     //
//	 В данном случае этот шаблон принимает в общей сложности 53 параметра, что должно быть постоянным                                                    //
//	 То есть нельзя удалить лишние кавычки, потому что так красивее                                                                                      //
//	 1 Параметр - Таблица ограничений                                                                                                                    //
//	 2 Параметр - Вид ограничения (Чтение, Добавление, Изменение), не указание вида = все 3 вида ограничений                                             //
//	 3 Параметр - Будет ли учитываться иерархия при наличии                                                                                              //
//	 4 Параметр - Текст соединений, все соединения пишутся в одной строке без символа '|'                                                                //
//	 7 - 53 Параметры - Условия ограничений.                                                                                                             //
//                                                                                                                                                       //
// 5. Необходимо добавить в форму документа в события ПриЧтенииНаСервере, ПослеЗаписинаСервере                                                           //
// 	  Следующие вызовы:                                                                                                                                  //
//                                                                                                                                                       //
//	  #Область ОбработчикиСобытий                                                                                                                        //
//	  &НаСервере                                                                                                                                         //
//	  Процедура ПриЧтенииНаСервере(ТекущийОбъект)                                                                                                        //
//	                                                                                                                                                     //
//	      УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);                                                                              //
//	                                                                                                                                                     //
//    КонецПроцедуры                                                                                                                                     //
//    &НаСервере                                                                                                                                         //
//    Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)                                                                                     //
//	                                                                                                                                                     //
//        УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);                                                           //
//	                                                                                                                                                     //
//    КонецПроцедуры                                                                                                                                     //
//    #КонецОбласти                                                                                                                                      //
//	                                                                                                                                                     //
// 	  С ними при наличии различных ограничений будет не доступа к редактированию форма при открытии                                                      //
//	  А также некоторые поля формы.                                                                                                                      //
//                                                                                                                                                       //
// 6. Добавить в модуль менеджера документа текст ограничения для производительного режима                                                               //
// 	  В процедуру ПриЗаполненииОграниченияДоступа, пример кода приведен ниже                                                                             //
//                                                                                                                                                       //
//	  Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт                                                                                     //
//        Ограничение.Текст =                                                                                                                            //
//        "ПрисоединитьДополнительныеТаблицы                                                                                                             //
//        |ЭтотСписок КАК ЭтотСписок                                                                                                                     //
//        |                                                                                                                                              //
//        |ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Уровень3Сотрудники                                                                                 //
//        |	ПО Уровень3Сотрудники.Ссылка = ЭтотСписок.Сотрудник                                                                                          //
//        |                                                                                                                                              //
//        |ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Уровень2Сотрудники                                                                                 //
//        |	ПО Уровень2Сотрудники.Ссылка = Уровень3Сотрудники.ГоловнойСотрудник                                                                          //
//        |                                                                                                                                              //
//        |ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Уровень1Сотрудники                                                                                 //
//        |	ПО Уровень1Сотрудники.Ссылка = Уровень2Сотрудники.ГоловнойСотрудник                                                                          //
//        |;                                                                                                                                             //
//        |РазрешитьЧтениеИзменение                                                                                                                      //
//        |ГДЕ                                                                                                                                           //
//        |	ЗначениеРазрешено(ЭтотСписок.СтруктурнаяЕдиница)                                                                                             //
//        |	И (ЗначениеРазрешено(Уровень3Сотрудники.Ссылка)                                                                                              //
//        |   ИЛИ ЗначениеРазрешено(Уровень2Сотрудники.Ссылка)                                                                                           //
//        |   ИЛИ ЗначениеРазрешено(Уровень1Сотрудники.Ссылка))";                                                                                        //
//    КонецПроцедуры                                                                                                                                     //
//                                                                                                                                                       //
//    Синтаксис данного ограничения описан в главе 3, раздела Управление доступом, документации                                                          //
//	  Поставки Библиотеки стандартных подсистем.                                                                                                         //
//                                                                                                                                                       //
// На этом включение собственного документа окончено                                                                                                     //
// Требуется также при необходимости обновить ключи досутпа и записи в служебных регистрах                                                               //
// С помощью текста для внешней обработки, и для профилактики воспользоваться обрабоктой                                                                 //
// УправлениеДоступом.epf, и отчетом ПроверкаВнедренияБСП.erf, с параметром 'Исправлять ошибки'                                                          //
//                                                                                                                                                       //
// Также при выполнении обмена данными ключи протухают только через 2 дня и чтобы не запускался                                                          //
// Длительный процесс обновления ключей следует либо установить костыль в модуле объета в событии ПриЗаписи                                              //
// Который отложит обновление ключей доступа на 2 дня (это предусмотрено в БСП) или дать разработать                                                     //
// План обмена не криворукому долб**бу.                                                                                                                  //
//                                                                                                                                                       //
// Конец кровопролитного душеизлияния в ходе разработки ограничений в УНФ 3.0 на версии БСП 3.1.8.438                                                    //
// p.p.s таких танцев с бубном на конфигурации с БСП версией 3.1.11.255 не обнаружил                                                                     //
//	                                                                                                                                                     //
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

&После("ПриЗаполненииСписковСОграничениемДоступа")
Процедура Пр_ПриЗаполненииСписковСОграничениемДоступа(Списки)
	
	Списки.Вставить(Метаданные.Документы.Пр_ОтчетЗаДень, Истина);

КонецПроцедуры   

&После("ПриЗаполненииПоставляемыхПрофилейГруппДоступа")
Процедура Пр_ПриЗаполненииПоставляемыхПрофилейГруппДоступа(ОписанияПрофилей, ПараметрыОбновления)
		
	ИмяПрофиля 			 = НСтр("ru = 'Программист'", ОбщегоНазначения.КодОсновногоЯзыка());
	ИдентификаторПрофиля = "81ff39a5-cf25-49d4-956c-1d625bed2709";

	ОписаниеПрофиля = УправлениеДоступом.НовоеОписаниеПрофиляГруппДоступа();
	ОписаниеПрофиля.Имя           = ИмяПрофиля;
	ОписаниеПрофиля.Идентификатор = ИдентификаторПрофиля;

	Для Каждого ТекОписание Из ОписанияПрофилей Цикл
		Если ТекОписание.Имя = ИмяПрофиля
			 И ТекОписание.Идентификатор = ИдентификаторПрофиля Тогда
			ОписаниеПрофиля = ТекОписание;
			Прервать;
		КонецЕсли;
	КонецЦикла;

	Если Не ТипЗнч(ОписаниеПрофиля) = Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеПрофиля.Наименование  = НСтр("ru = 'Программист'", ОбщегоНазначения.КодОсновногоЯзыка()); 
	
	// Права доступа InfostartToolkitCorp
	
	ОписаниеПрофиля.Роли.Добавить("ИТК_АнализПравДоступа"); 
	ОписаниеПрофиля.Роли.Добавить("ИТК_ВсеФункции");
	ОписаниеПрофиля.Роли.Добавить("ИТК_ЖурналРегистрации");
	ОписаниеПрофиля.Роли.Добавить("ИТК_КонсольКода");
	ОписаниеПрофиля.Роли.Добавить("ИТК_КонсольРазработчика");
	ОписаниеПрофиля.Роли.Добавить("ИТК_Метаданные");  
	ОписаниеПрофиля.Роли.Добавить("ИТК_МониторЛицензий"); 
	ОписаниеПрофиля.Роли.Добавить("ИТК_ОбслуживаниеСлужебные");
	ОписаниеПрофиля.Роли.Добавить("ИТК_ОсновныеПрава");
	ОписаниеПрофиля.Роли.Добавить("ИТК_ПодпискиНаСобытия");
	ОписаниеПрофиля.Роли.Добавить("ИТК_ПоискИЗаменаСсылок");
	ОписаниеПрофиля.Роли.Добавить("ИТК_ПоискПоСтруктуреХранения");  	
	ОписаниеПрофиля.Роли.Добавить("ИТК_ПоискСсылокНаОбъект");
	ОписаниеПрофиля.Роли.Добавить("ИТК_ПолныеПрава");
	ОписаниеПрофиля.Роли.Добавить("ИТК_Пользователи");
	ОписаниеПрофиля.Роли.Добавить("ИТК_РасширениеГлобальногоПоиска");  
	ОписаниеПрофиля.Роли.Добавить("ИТК_РегламентныеИФоновыеЗадания"); 
	ОписаниеПрофиля.Роли.Добавить("ИТК_РедакторЗаписиНезависимогоРегистраСведений");
	ОписаниеПрофиля.Роли.Добавить("ИТК_РедакторКонстант");
	ОписаниеПрофиля.Роли.Добавить("ИТК_РедакторОбъекта");
	ОписаниеПрофиля.Роли.Добавить("ИТК_РедакторПараметровСеанса");
	ОписаниеПрофиля.Роли.Добавить("ИТК_СравнениеОбъектов"); 
	ОписаниеПрофиля.Роли.Добавить("ИТК_СтруктураХранения");   
	
	// Конец Права доступа InfostartToolkitCorp
		
	// Права основной конфигурации
	ОписаниеПрофиля.Роли.Добавить("БазовыеПраваЗЕРНО");	
	ОписаниеПрофиля.Роли.Добавить("БазовыеПраваЗарплатаКадры");	
	ОписаниеПрофиля.Роли.Добавить("БазовыеПраваБИД");
	ОписаниеПрофиля.Роли.Добавить("БазовыеПраваБПО");
	ОписаниеПрофиля.Роли.Добавить("БазовыеПраваБРО");
	ОписаниеПрофиля.Роли.Добавить("БазовыеПраваБСП");
	ОписаниеПрофиля.Роли.Добавить("БазовыеПраваБТС");
	ОписаниеПрофиля.Роли.Добавить("БазовыеПраваВЕТИС");
	ОписаниеПрофиля.Роли.Добавить("БазовыеПраваГИСМ");
	ОписаниеПрофиля.Роли.Добавить("БазовыеПраваЕГАИС");
	ОписаниеПрофиля.Роли.Добавить("БазовыеПраваИПП");
	ОписаниеПрофиля.Роли.Добавить("БазовыеПраваИСМП");
	ОписаниеПрофиля.Роли.Добавить("БазовыеПраваПК");
	ОписаниеПрофиля.Роли.Добавить("БазовыеПраваЭД");
	ОписаниеПрофиля.Роли.Добавить("БазовыеПраваПриложения");
	ОписаниеПрофиля.Роли.Добавить("БазовыеПраваСервисДоставки");
	ОписаниеПрофиля.Роли.Добавить("БазовыеПраваЗЕРНО");
	ОписаниеПрофиля.Роли.Добавить("ВыводНаПринтерФайлБуферОбмена");
	ОписаниеПрофиля.Роли.Добавить("ВызовОнлайнПоддержки");
	ОписаниеПрофиля.Роли.Добавить("ЗапускAutomation");
	ОписаниеПрофиля.Роли.Добавить("ЗапускВебКлиента");
	ОписаниеПрофиля.Роли.Добавить("ЗапускВнешнегоСоединения");
	ОписаниеПрофиля.Роли.Добавить("ЗапускМобильногоКлиента");
	ОписаниеПрофиля.Роли.Добавить("ЗапускТолстогоКлиента");
	ОписаниеПрофиля.Роли.Добавить("ЗапускТонкогоКлиента");
	ОписаниеПрофиля.Роли.Добавить("ИзменениеДополнительныхСведений");  
	ОписаниеПрофиля.Роли.Добавить("ИзменениеВыполнениеЗадач");  
	ОписаниеПрофиля.Роли.Добавить("ИзменениеМакетовПечатныхФорм");  
	ОписаниеПрофиля.Роли.Добавить("ИзменениеНастроекПользователей");  
	ОписаниеПрофиля.Роли.Добавить("ИспользованиеОбсуждений");  
	ОписаниеПрофиля.Роли.Добавить("ПодключениеИнтернетПоддержки");  
	ОписаниеПрофиля.Роли.Добавить("РаботаСФайлами");  
	ОписаниеПрофиля.Роли.Добавить("РаспознаваниеДокументов");  
	ОписаниеПрофиля.Роли.Добавить("РасшифровкаДанных");  
	ОписаниеПрофиля.Роли.Добавить("РежимТехническогоСпециалиста");  
	ОписаниеПрофиля.Роли.Добавить("СохранениеДанныхПользователя");  
	ОписаниеПрофиля.Роли.Добавить("ШифрованиеИРасшифровкаДанных");  
	// Конец Права основной конфигурации
	
	// Права расширения GitLabCorp
	
	ОписаниеПрофиля.Роли.Добавить("Пр_ОсновнаяРоль"); 
	ОписаниеПрофиля.Роли.Добавить("Пр_ПросмотрКорпоративнаяСистемаGitLab");  
	ОписаниеПрофиля.Роли.Добавить("Пр_ПолучениеУстановкаТекущийСотрудник");  
	ОписаниеПрофиля.Роли.Добавить("Пр_ПолучениеУстановкаТекущееПодразделение");  
	ОписаниеПрофиля.Роли.Добавить("Пр_ДобавлениеИзменениеОбменGitLab"); 
	ОписаниеПрофиля.Роли.Добавить("Пр_ПросмотрПланФактовыйАнализ"); 
	ОписаниеПрофиля.Роли.Добавить("Пр_ПросмотрПровестиОпрос");  
	ОписаниеПрофиля.Роли.Добавить("Пр_ИспользованиеUpdateIssue");  
	ОписаниеПрофиля.Роли.Добавить("Пр_ИспользованиеGetMessage");  
	ОписаниеПрофиля.Роли.Добавить("Пр_РедактированиеАдресСервераGitLab");
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеВидовНачисленийИУдержаний");
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеВидовЦен");
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеВидовЦенКонтрагентов");
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеГрафиковИРесурсов");
	ОписаниеПрофиля.Роли.Добавить("ИзменениеУчастниковГруппДоступа");
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеКонтрагентов");
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеДолжностей");
	ОписаниеПрофиля.Роли.Добавить("ИспользованиеКалендаряСотрудника");
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеКасс");
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеНоменклатуры");
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеНаправленийДеятельности");	
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеПользователей");
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеПроектов");
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеСобытийИЗаданий");
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеСотрудников");
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеСтруктурныхЕдиниц");	
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеАктовВыполненныхРабот");
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеНачисленийЗарплаты");
	ОписаниеПрофиля.Роли.Добавить("Пр_ДобавлениеИзменениеОтчетЗаДень");
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеСчетовНаОплату");
	ОписаниеПрофиля.Роли.Добавить("Пр_ИспользованиеПланФактовоеОтклонениеЗаданий");	
	ОписаниеПрофиля.Роли.Добавить("Пр_ИспользованиеАнализПрибыльности");
	ОписаниеПрофиля.Роли.Добавить("Пр_ИспользованиеОтладкаИнтеграции");
	ОписаниеПрофиля.Роли.Добавить("Пр_ИспользованиеФормированиеПакетаДокументов");
	ОписаниеПрофиля.Роли.Добавить("Пр_ДобавлениеИзменениеЛимитыПоКолонкам");
	ОписаниеПрофиля.Роли.Добавить("Пр_ДобавлениеИзменениеПланФактовоеОтклонение");
	ОписаниеПрофиля.Роли.Добавить("Пр_ДобавлениеИзменениеНеотправленныеСообщенияGitLab");
	ОписаниеПрофиля.Роли.Добавить("Пр_ДобавлениеИзменениеЕжедневныйОпрос");
	ОписаниеПрофиля.Роли.Добавить("ИспользованиеКонтактЦентра");
	ОписаниеПрофиля.Роли.Добавить("Пр_ДобавлениеИзменениеУправленческий");
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеВалют");
	ОписаниеПрофиля.Роли.Добавить("ИзменениеТекущихКадровыхДанныхСотрудников");
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеЦенНоменклатуры");
	
	// Конец Права расширения GitLabCorp
	
	// Виды ограничения доступа профиля.
	ОписаниеПрофиля.ВидыДоступа.Добавить("Сотрудники", "ВначалеВсеРазрешены");
	ОписаниеПрофиля.ВидыДоступа.Добавить("СтруктурныеЕдиницы", "ВначалеВсеРазрешены");      
	
	// Включаем возможность изменения профилей поставки
	ПараметрыОбновления.ЗапретитьИзменениеПрофилей = Ложь;
	
	ОписаниеПрофиля.Описание = НСтр(
	"ru = 'Под профилем осуществляется работа с подсистемой Корпоративная система GitLab.
	|
	|Предназначен для:
	|- программистов (доступ ко всем данным) и администраторов,
	|- аналитиков - формирование заданий на разработку
	|- руководителей - просмотр аналитики и формирование отчетных документов.'");

	ОписанияПрофилей.Добавить(ОписаниеПрофиля); 
	
КонецПроцедуры

&После("ПриЗаполненииВидовДоступа")
Процедура Пр_ПриЗаполненииВидовДоступа(ВидыДоступа)
	
	ВидДоступа = ВидыДоступа.Добавить();
    ВидДоступа.Имя = "Сотрудники";
    ВидДоступа.Представление = НСтр("ru = 'Сотрудники'");
    ВидДоступа.ТипЗначений   = Тип("СправочникСсылка.Сотрудники");   
	
	ВидДоступа = ВидыДоступа.Добавить();
    ВидДоступа.Имя = "СтруктурныеЕдиницы";
    ВидДоступа.Представление = НСтр("ru = 'Структурные единицы'");
    ВидДоступа.ТипЗначений   = Тип("СправочникСсылка.СтруктурныеЕдиницы");
	
КонецПроцедуры

&После("ПриЗаполненииИспользованияВидаДоступа")
Процедура Пр_ПриЗаполненииИспользованияВидаДоступа(ВидДоступа, Использование)
	
	// Наличие только этого признака не дает гарантии того что вид доступа все таки
	// Будет доступен к выбору. Также следует обратить внимание на регистр сведений ИспользуемыеВидыДоступа
	// В него могут не попасть добавленные вручную виды доступа. Способа инициации инструментами
	// Разработчика в поставке БСП не обнаружил. Для того чтобы виды доступа появились гарантировано
	// Следует дернуть вручную процедуру в обработке у менеджера регистра сведений
	// РегистрСведений.ИспользуемыеВидыДоступа.ОбновитьДанныеРегистра с двумя флагами ИСТИНА
	Если ВидДоступа = "Сотрудники" Тогда
	    Использование = Истина;
	КонецЕсли;
	
КонецПроцедуры

&После("ПриЗаполненииВидовОграниченийПравОбъектовМетаданных")
Процедура Пр_ПриЗаполненииВидовОграниченийПравОбъектовМетаданных(Описание)
	
	Описание = Описание + 
		"Документ.Пр_ОтчетЗаДень.Чтение.Сотрудники
		|Документ.Пр_ОтчетЗаДень.Изменение.Сотрудники
		|Документ.Пр_ОтчетЗаДень.Чтение.СтруктурныеЕдиницы
		|Документ.Пр_ОтчетЗаДень.Изменение.СтруктурныеЕдиницы";
	
КонецПроцедуры

#КонецОбласти
