#Область ПрограммныйИнтерфейс

// Определение и обработка создания формы
//
// Параметры:
// ЭтотОбъект - ФормаКлиентскогоПриложения
// СтандартнаяОбработка - Булево
// Отказ - Булево
// 
Процедура ПриСозданииНаСервере(ЭтотОбъект, СтандартнаяОбработка, Отказ) Экспорт
	
	Если ЭтотОбъект.ИмяФормы = "Документ.ЗаданиеНаРаботу.Форма.ФормаДокумента" Тогда
		Документ_ЗаданиеНаРаботу_Форма_ФормаЭлемента(ЭтотОбъект, СтандартнаяОбработка, Отказ);	 
	ИначеЕсли ЭтотОбъект.ИмяФормы = "Справочник.Проекты.Форма.ФормаЭлемента" Тогда
		Справочник_Проекты_Форма_ФормаЭлемента(ЭтотОбъект, СтандартнаяОбработка, Отказ);
	ИначеЕсли ЭтотОбъект.ИмяФормы = "Документ.НачислениеЗарплатыУНФ.Форма.ФормаДокумента" Тогда
		Документ_НачислениеЗарплатыУНФ_Форма_ФормаДокумента(ЭтотОбъект, СтандартнаяОбработка, Отказ);
	ИначеЕсли ЭтотОбъект.ИмяФормы = "Справочник.ВидыЦенКонтрагентов.Форма.ФормаЭлемента" Тогда
		Справочник_ВидыЦенКонтрагентов_Форма_ФормаЭлемента(ЭтотОбъект, СтандартнаяОбработка, Отказ);
	ИначеЕсли ЭтотОбъект.ИмяФормы = "Справочник.Пользователи.Форма.ФормаЭлемента" Тогда
		Справочник_Пользователи_Форма_ФормаЭлемента(ЭтотОбъект, СтандартнаяОбработка, Отказ); 
	ИначеЕсли ЭтотОбъект.ИмяФормы = "Обработка.КонтактЦентр.Форма.Форма" Тогда
		Обработка_КонтактЦентр_Форма_Форма(ЭтотОбъект, СтандартнаяОбработка, Отказ);
	КонецЕсли;
	
КонецПроцедуры   

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Обработка создания формы документа ЗаданиеНаРаботу
//
// Параметры:
// ЭтотОбъект - ФормаКлиентскогоПриложения
// СтандартнаяОбработка - Булево
// Отказ - Булево
// 
Процедура Документ_ЗаданиеНаРаботу_Форма_ФормаЭлемента(ЭтотОбъект, СтандартнаяОбработка, Отказ)

	Элементы = ЭтотОбъект.Элементы;
	
	ДобавляемыеРеквизиты = Новый Массив; 
	СтатусЗадания = Новый РеквизитФормы("Пр_Статус",
										ОбщегоНазначения.ОписаниеТипаСтрока(0),,
										НСтр("ru = 'Статус'"));
	ДобавляемыеРеквизиты.Добавить(СтатусЗадания);
	ЭтотОбъект.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	Элементы.ГруппаВидИСостояние.Видимость = Ложь;
	Элементы.ГруппаЗаданиеУтвердил.Видимость = Ложь;
	Элементы.КоманднаяПанельРедактироватьСписком.Видимость = Ложь;
	Элементы.ПланированиеЗаказчикРабота.Видимость = Ложь;
	
	НовыйЭлемент = Элементы.Вставить("Пр_Статус", Тип("ПолеФормы"), Элементы.ЛеваяКолонка, Элементы.Сотрудник);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеНадписи;
	НовыйЭлемент.ПутьКДанным = "Пр_Статус";
	НовыйЭлемент.ЦветФона = WebЦвета.Голубой; 
	
	Элементы.ЛеваяКолонка.РастягиватьПоГоризонтали = Ложь;
	
	ГруппаПраваяКолонка = Элементы.Добавить("Пр_ПраваяКолонка", Тип("ГруппаФормы"), Элементы.Шапка);
	ГруппаПраваяКолонка.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаПраваяКолонка.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	ГруппаПраваяКолонка.РастягиватьПоГоризонтали = Ложь;
	ГруппаПраваяКолонка.ОтображатьЗаголовок = Ложь;
	
	ГруппаПраваяКолонка1 = Элементы.Добавить("Пр_ПраваяКолонка1", Тип("ГруппаФормы"), ГруппаПраваяКолонка);
	ГруппаПраваяКолонка1.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаПраваяКолонка1.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;    
	ГруппаПраваяКолонка1.ОтображатьЗаголовок = Ложь;
	
	ГруппаПраваяКолонка2 = Элементы.Добавить("Пр_ПраваяКолонка2", Тип("ГруппаФормы"), ГруппаПраваяКолонка);
	ГруппаПраваяКолонка2.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаПраваяКолонка2.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная; 
	ГруппаПраваяКолонка2.ОтображатьЗаголовок = Ложь;
	
	Элементы.Шапка.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	
	НовыйЭлемент = Элементы.Добавить("Пр_Проект", Тип("ПолеФормы"), ГруппаПраваяКолонка1);  
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "Объект.Пр_Проект";      
	НовыйЭлемент.Заголовок = НСтр("ru = 'Проект'");
	НовыйЭлемент.УстановитьДействие("ОбработкаВыбора", "Пр_ПроектОбработкаВыбора");
	
	НовыйЭлемент = Элементы.Добавить("Пр_Идентификатор", Тип("ПолеФормы"),ГруппаПраваяКолонка1);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "Объект.Пр_Идентификатор";
	НовыйЭлемент.Заголовок = НСтр("ru = 'Идентификатор GitLab'");
	НовыйЭлемент.ТолькоПросмотр = Истина; 
	
	НовыйЭлемент = Элементы.Добавить("Пр_ОценкаВGitLab", Тип("ПолеФормы"), ГруппаПраваяКолонка2);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "Объект.Пр_ОценкаВGitLab";
	НовыйЭлемент.Заголовок = НСтр("ru = 'Оценка в GitLab (Часы)'");
	НовыйЭлемент.ТолькоПросмотр = Истина;
	
	НовыйЭлемент = Элементы.Добавить("Пр_ФактВGitLab", Тип("ПолеФормы"), ГруппаПраваяКолонка2);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "Объект.Пр_ФактВGitLab";
	НовыйЭлемент.Заголовок = НСтр("ru = 'Факт в GitLab (Часы)'");
	НовыйЭлемент.ТолькоПросмотр = Истина;
	
КонецПроцедуры

// Обработка создания формы справочника Проекты
//
// Параметры:
// ЭтотОбъекта - ФормаКлиентскогоПриложения
// СтандартнаяОбработка - Булево
// Отказ - Булево
//
Процедура Справочник_Проекты_Форма_ФормаЭлемента(ЭтотОбъект, СтандартнаяОбработка, Отказ)

	Элементы = ЭтотОбъект.Элементы;
	
	НовыйЭлемент = Элементы.Добавить("Пр_ТокенДоступа", Тип("ПолеФормы"), Элементы.Реквизиты);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "Объект.Пр_ТокенДоступа";
	НовыйЭлемент.Заголовок = НСтр("ru = 'Токен доступа GitLab'"); 
	
	НовыйЭлемент = Элементы.Добавить("Пр_НомерПроекта", Тип("ПолеФормы"), Элементы.Реквизиты);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "Объект.Пр_НомерПроекта";
	НовыйЭлемент.Заголовок = НСтр("ru = 'Номер проекта GitLab'");
	
КонецПроцедуры

// Обработка создания формы документа НачислениеЗарплатыУНФ
//
// Параметры:
// ЭтотОбъекта - ФормаКлиентскогоПриложения
// СтандартнаяОбработка - Булево
// Отказ - Булево
//
Процедура Документ_НачислениеЗарплатыУНФ_Форма_ФормаДокумента(ЭтотОбъект, СтандартнаяОбработка, Отказ)

	Элементы = ЭтотОбъект.Элементы;
	
	КомандаФормы = ЭтотОбъект.Команды.Добавить("Пр_РассчитатьЗППрограммистов");
	КомандаФормы.Действие = "Пр_РассчитатьЗППрограммистов";
	КомандаФормы.Заголовок = "Рассчитать ЗП программистов";
	
	КнопкаФормы = Элементы.Добавить("Пр_РассчитатьЗППрограммистов", Тип("КнопкаФормы"), Элементы.ГруппаКомандыРасчет);
	КнопкаФормы.ИмяКоманды = КомандаФормы.Имя;
	КнопкаФормы.Заголовок = КомандаФормы.Заголовок;
	КнопкаФормы.Шрифт = Новый Шрифт("Times New Roman", 11, Истина);
	КнопкаФормы.ЦветФона = WebЦвета.Розовый;
	
КонецПроцедуры

// Обработка создания формы справочника ВидыЦенКонтрагентов
//
// Параметры:
// ЭтотОбъекта - ФормаКлиентскогоПриложения
// СтандартнаяОбработка - Булево
// Отказ - Булево
//
Процедура Справочник_ВидыЦенКонтрагентов_Форма_ФормаЭлемента(ЭтотОбъект, СтандартнаяОбработка, Отказ)

	Элементы = ЭтотОбъект.Элементы;
	
	НовыйЭлемент = Элементы.Добавить("Пр_Проект", Тип("ПолеФормы"), Элементы.НаименованиеКод);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "Объект.Пр_Проект";
	НовыйЭлемент.Заголовок = НСтр("ru = 'Проект'"); 
		
КонецПроцедуры  

// Обработка создания формы справочника Пользователи
//
// Параметры:
// ЭтотОбъекта - ФормаКлиентскогоПриложения
// СтандартнаяОбработка - Булево
// Отказ - Булево
//
Процедура Справочник_Пользователи_Форма_ФормаЭлемента(ЭтотОбъект, СтандартнаяОбработка, Отказ)

	Элементы = ЭтотОбъект.Элементы;
	
	НовыйЭлемент = Элементы.Добавить("Пр_ТокенДоступа", Тип("ПолеФормы"), Элементы.ИмяЭлектроннаяПочтаТелефон);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "Объект.Пр_ТокенДоступа";
	НовыйЭлемент.Заголовок = НСтр("ru = 'Токен доступа'"); 
		
КонецПроцедуры

// Обработка создания формы обработки КонтактЦентр
//
// Параметры:
// ЭтотОбъекта - ФормаКлиентскогоПриложения
// СтандартнаяОбработка - Булево
// Отказ - Булево
//
Процедура Обработка_КонтактЦентр_Форма_Форма(ЭтотОбъект, СтандартнаяОбработка, Отказ)
	
	Элементы = ЭтотОбъект.Элементы;
	Элементы.ФормаНастроить.АвтоМаксимальнаяШирина = Ложь;  
	Элементы.ФормаНастроить.Ширина = 4;
	
	ДобавляемыеРеквизиты = Новый Массив;
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("Пр_УведомлениеОДлительномАнализе",
								  Новый ОписаниеТипов("Булево"),, 
								  НСтр("ru = 'Уведомление о длительном анализе'"))); 
								  
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("Пр_МаксимальныйСрокАнализаДни",
								  ОбщегоНазначения.ОписаниеТипаЧисло(3, 0, ДопустимыйЗнак.Неотрицательный),, 
								  НСтр("ru = 'Максимальный срок анализа'")));   
								  
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("Пр_ПользовательДляУведомленияАнализ",
								  Новый ОписаниеТипов("СправочникСсылка.Пользователи"),, 
								  НСтр("ru = 'Пользователь для уведомления'"))); 
								  
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("Пр_УведомлениеОДлительнойРазработке",
								  Новый ОписаниеТипов("Булево"),, 
								  НСтр("ru = 'Уведомление о длительной разработке'"))); 
								  
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("Пр_МаксимальныйСрокРазработкиДни",
								  ОбщегоНазначения.ОписаниеТипаЧисло(3, 0, ДопустимыйЗнак.Неотрицательный),, 
								  НСтр("ru = 'Максимальный срок разработки'")));   
								  
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("Пр_ПользовательДляУведомленияРазработка",
								  Новый ОписаниеТипов("СправочникСсылка.Пользователи"),, 
								  НСтр("ru = 'Пользователь для уведомления'"))); 
								  
	ЭтотОбъект.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	НоваяГруппа = Элементы.Добавить("Пр_НастройкаУведомлений", Тип("ГруппаФормы"), Элементы.ГруппаКоманднаяПанель);    
	НоваяГруппа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НоваяГруппа.Заголовок = НСтр("ru = 'Настройка уведомлений'");
	НоваяГруппа.Поведение = ПоведениеОбычнойГруппы.Всплывающая;
	НоваяГруппа.ОтображениеУправления = ОтображениеУправленияОбычнойГруппы.ГиперссылкаЗаголовка;
	НоваяГруппа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	НоваяГруппа.ВертикальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Одинарный;
	НоваяГруппа.ЦветТекстаЗаголовка = ЦветаСтиля.ЦветТекстаФормы;
	НоваяГруппа.ШрифтЗаголовка = ШрифтыСтиля.ОбычныйШрифтТекста;
	НоваяГруппа.ВертикальноеПоложениеВГруппе = ВертикальноеПоложениеЭлемента.Центр;
	
	ГруппаЭлементы = Элементы.Добавить("Пр_ПоляНастроек", Тип("ГруппаФормы"), НоваяГруппа);
	ГруппаЭлементы.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаЭлементы.РастягиватьПоГоризонтали = Ложь;                          
	ГруппаЭлементы.ВыравниваниеЭлементовИЗаголовков = ВариантВыравниванияЭлементовИЗаголовков.ЭлементыПравоЗаголовкиЛево;
	ГруппаЭлементы.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
    ГруппаЭлементы.ОтображатьЗаголовок = Ложь;
		
	
	НовыйЭлемент = Элементы.Добавить("Пр_УведомлениеОДлительномАнализе", Тип("ПолеФормы"), ГруппаЭлементы);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеФлажка;
	НовыйЭлемент.ПутьКДанным = "Пр_УведомлениеОДлительномАнализе";   
	НовыйЭлемент.УстановитьДействие("ПриИзменении", "УстановитьНастройкуУведомлений");
	
	НовыйЭлемент = Элементы.Добавить("Пр_МаксимальныйСрокАнализаДни", Тип("ПолеФормы"), ГруппаЭлементы);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "Пр_МаксимальныйСрокАнализаДни";
	НовыйЭлемент.УстановитьДействие("ПриИзменении", "УстановитьНастройкуУведомлений");

	НовыйЭлемент = Элементы.Добавить("Пр_ПользовательДляУведомленияАнализ", Тип("ПолеФормы"), ГруппаЭлементы);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "Пр_ПользовательДляУведомленияАнализ"; 
	НовыйЭлемент.УстановитьДействие("ПриИзменении", "УстановитьНастройкуУведомлений");

	НовыйЭлемент = Элементы.Добавить("Пр_УведомлениеОДлительнойРазработке", Тип("ПолеФормы"), ГруппаЭлементы);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеФлажка;
	НовыйЭлемент.ПутьКДанным = "Пр_УведомлениеОДлительнойРазработке"; 
	НовыйЭлемент.УстановитьДействие("ПриИзменении", "УстановитьНастройкуУведомлений");
	
	НовыйЭлемент = Элементы.Добавить("Пр_МаксимальныйСрокРазработкиДни", Тип("ПолеФормы"), ГруппаЭлементы);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "Пр_МаксимальныйСрокРазработкиДни";
	НовыйЭлемент.УстановитьДействие("ПриИзменении", "УстановитьНастройкуУведомлений");

	НовыйЭлемент = Элементы.Добавить("Пр_ПользовательДляУведомленияРазработка", Тип("ПолеФормы"), ГруппаЭлементы);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "Пр_ПользовательДляУведомленияРазработка"; 
	НовыйЭлемент.УстановитьДействие("ПриИзменении", "УстановитьНастройкуУведомлений");

	КомандаФормы = ЭтотОбъект.Команды.Добавить("Пр_ИзменитьТекстУведомления");
	КомандаФормы.Заголовок = НСтр("ru = 'Изменить текст уведомления'");
	КомандаФормы.Действие = "Пр_ИзменитьТекстУведомления"; 
	
	КнопкаФормы = Элементы.Добавить("Пр_ИзменитьТекстУведомления", Тип("КнопкаФормы"), ГруппаЭлементы);
	КнопкаФормы.Заголовок = КомандаФормы.Заголовок;
	КнопкаФормы.ИмяКоманды = КомандаФормы.Имя;
	КнопкаФормы.ЦветФона = ЦветаСтиля.ЦветВажного;
	КнопкаФормы.Шрифт = ШрифтыСтиля.ВажнаяНадписьШрифт;
	
КонецПроцедуры      

#КонецОбласти