#Область ПрограммныйИнтерфейс

Функция ПолучитьСообщения() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	МассивСообщений = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Пр_ОповещенияПользователейСрезПоследних.Период КАК Период,
		|	Пр_ОповещенияПользователейСрезПоследних.Источник КАК Источник,
		|	Пр_ОповещенияПользователейСрезПоследних.Получатель КАК Получатель,
		|	Пр_ОповещенияПользователейСрезПоследних.ТекстСообщения КАК ТекстСообщения,
		|	Пр_ОповещенияПользователейСрезПоследних.Отправлено КАК Отправлено
		|ИЗ
		|	РегистрСведений.Пр_ОповещенияПользователей.СрезПоследних(
		|			&Дата,
		|			Получатель = &Получатель
		|				И НЕ Отправлено) КАК Пр_ОповещенияПользователейСрезПоследних";
	
	Запрос.УстановитьПараметр("Дата", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("Получатель", ПараметрыСеанса.ТекущийПользователь);
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Для Каждого Сообщение Из РезультатЗапроса Цикл               
		СтруктураСообщения = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(Сообщение);
		СтруктураСообщения.Вставить("НавигационнаяСсылка", ПолучитьНавигационнуюСсылку(Сообщение.Источник));
		СтруктураСообщения.Вставить("УУИДСообщения", Новый УникальныйИдентификатор());
		МассивСообщений.Добавить(СтруктураСообщения);		
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат МассивСообщений;
	
КонецФункции

Процедура ПрочитатьСообщения(МассивСообщений) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Для Каждого Сообщение Из МассивСообщений Цикл
		
		МенеджерСообщения = РегистрыСведений.Пр_ОповещенияПользователей.СоздатьМенеджерЗаписи();
        ЗаполнитьЗначенияСвойств(МенеджерСообщения, Сообщение);
		МенеджерСообщения.Прочитать();
		Если МенеджерСообщения.Выбран() Тогда
			МенеджерСообщения.Отправлено = Истина;
			МенеджерСообщения.Записать(Истина);    
			
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Уведомления просроченых заданий'"),
									 УровеньЖурналаРегистрации.Информация,
									 Метаданные.Документы.ЗаданиеНаРаботу,
									 Сообщение.Источник,
									 НСтр("ru = 'Сообщение отправлено!'"));

		КонецЕсли;
				
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти