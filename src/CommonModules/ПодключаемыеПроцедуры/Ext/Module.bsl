// Процедура заполняет произвольные данные для показателя
//
// Параметры:
//  Показатель			 - СправочникСсылка.ПоказателиБизнеса	 - показатель по которому получаем данные
//  ПараметрыОтчета		 - Структура - параметры формирования отчета
//   * ПериодОтчета		 - СтандартныйПериод - период за который формируется отчет
//   * Периодичность	 - ПеречислениеСсылка.Периодичность - выбранная периодичность: месяц, квартал, год
//   * ПланФакт			 - ПеречислениеСсылка.ПланФакт - вариант отчета: план, факт, план/факт
//   * Сценарий			 - СправочникСсылка.СценарииПланирования - сценарий для вариантов план и план/факт
//   * Группировка		 - ПеречислениеСсылка.ГруппировкаАнализаБизнеса - вариант группировки по аналитике
//   * Отбор			 - Структура - отборы установленные в отчете
//  ТаблицаРезультата	 - ТаблицаЗначений	 - таблица для помещения результата. Колонки:
//   * Период			 - Дата - период к которому относится сумма по строке
//   * Аналитика		 - Произвольный - аналитика показателя: проекты или подразделения
//   * Сумма			 - Число - сумма показателя
//
Процедура ПолучитьПоказатель_fe066537_806c_11f0_945b_985f41b2a73b(Показатель, ПараметрыОтчета, ТаблицаРезультата) Экспорт
	
	Запрос = Новый Запрос; 
	НеИспользоватьОтборПроект = Не ПараметрыОтчета.Отбор.Свойство("Проект");
	МассивПроектов = Новый Массив;
	Если Не НеИспользоватьОтборПроект Тогда
		МассивПроектов = ПараметрыОтчета.Отбор.Проект;	
	КонецЕсли;
	
	Если ПараметрыОтчета.ПланФакт = Перечисления.ПланФакт.Факт Тогда  
		
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	НАЧАЛОПЕРИОДА(Задание.Дата, МЕСЯЦ) КАК Период,
			|	Т.Проект КАК Аналитика,
			|	СУММА(Т.ФактическиеЗатраты) КАК Сумма
			|ИЗ
			|	РегистрСведений.Пр_ПланФактовоеОтклонение КАК Т
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаданиеНаРаботу КАК Задание
			|		ПО Т.ЗаданиеНаРаботу = Задание.Ссылка
			|ГДЕ
			|	Задание.Дата МЕЖДУ &НачалоПериода И &КонецПериода
			|	И (&НеИспользоватьОтборПроект
			|			ИЛИ Т.Проект В (&МассивПроектов))
			|
			|СГРУППИРОВАТЬ ПО
			|	НАЧАЛОПЕРИОДА(Задание.Дата, МЕСЯЦ),
			|	Т.Проект";   
		
		Запрос.УстановитьПараметр("НачалоПериода", ПараметрыОтчета.ПериодОтчета.ДатаНачала);
	    Запрос.УстановитьПараметр("КонецПериода", ПараметрыОтчета.ПериодОтчета.ДатаОкончания);  
		Запрос.УстановитьПараметр("НеИспользоватьОтборПроект", НеИспользоватьОтборПроект);
		Запрос.УстановитьПараметр("МассивПроектов", МассивПроектов);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			НовыйРезультат = ТаблицаРезультата.Добавить();     
			НовыйРезультат.Показатель = Показатель;
			НовыйРезультат.ИдентификаторПоказателя = "fe066537_806c_11f0_945b_985f41b2a73b";
			НовыйРезультат.Аналитика = Выборка.Аналитика;
			НовыйРезультат.Период = Выборка.Период;
			НовыйРезультат.Сумма = Выборка.Сумма;
		КонецЦикла;
		
	КонецЕсли;
	
	Если ПараметрыОтчета.ПланФакт = Перечисления.ПланФакт.План Тогда  
		
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Т.Месяц КАК Месяц,
			|	Т.Проект КАК Проект,
			|	СУММА(Т.КоличествоЧасов) КАК КоличествоЧасов
			|ИЗ
			|	РегистрСведений.Пр_ПланФактоваяВыработка.СрезПоследних(
			|			,
			|			Месяц МЕЖДУ &НачалоПериода И &КонецПериода
			|				И (&НеИспользоватьОтборПроект
			|					ИЛИ Проект В (&МассивПроектов))) КАК Т
			|
			|СГРУППИРОВАТЬ ПО
			|	Т.Проект,
			|	Т.Месяц";   
		
		Запрос.УстановитьПараметр("НачалоПериода", ПараметрыОтчета.ПериодОтчета.ДатаНачала);
	    Запрос.УстановитьПараметр("КонецПериода", ПараметрыОтчета.ПериодОтчета.ДатаОкончания);
		Запрос.УстановитьПараметр("НеИспользоватьОтборПроект", НеИспользоватьОтборПроект);
		Запрос.УстановитьПараметр("МассивПроектов", МассивПроектов);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			НовыйРезультат = ТаблицаРезультата.Добавить();     
			НовыйРезультат.Показатель = Показатель;
			НовыйРезультат.ИдентификаторПоказателя = "fe066537_806c_11f0_945b_985f41b2a73b";
			НовыйРезультат.Аналитика = Выборка.Проект;
			НовыйРезультат.Период = Выборка.Месяц;
			НовыйРезультат.Сумма = Выборка.КоличествоЧасов;
		КонецЦикла;
		
	КонецЕсли;

	Если ПараметрыОтчета.ПланФакт = Перечисления.ПланФакт.ПланФакт Тогда  
		
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	НАЧАЛОПЕРИОДА(Задание.Дата, МЕСЯЦ) КАК Период,
			|	Т.Проект КАК Аналитика,
			|	СУММА(Т.ФактическиеЗатраты) КАК Сумма
			|ПОМЕСТИТЬ ВТ_Факт
			|ИЗ
			|	РегистрСведений.Пр_ПланФактовоеОтклонение КАК Т
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаданиеНаРаботу КАК Задание
			|		ПО Т.ЗаданиеНаРаботу = Задание.Ссылка
			|ГДЕ
			|	Задание.Дата МЕЖДУ &НачалоПериода И &КонецПериода
			|	И (&НеИспользоватьОтборПроект
			|			ИЛИ Т.Проект В (&МассивПроектов))
			|
			|СГРУППИРОВАТЬ ПО
			|	НАЧАЛОПЕРИОДА(Задание.Дата, МЕСЯЦ),
			|	Т.Проект
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Период,
			|	Аналитика
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Т.Месяц КАК Месяц,
			|	Т.Проект КАК Проект,
			|	СУММА(Т.КоличествоЧасов) КАК КоличествоЧасов
			|ПОМЕСТИТЬ ВТ_План
			|ИЗ
			|	РегистрСведений.Пр_ПланФактоваяВыработка.СрезПоследних(
			|			,
			|			Месяц МЕЖДУ &НачалоПериода И &КонецПериода
			|				И (&НеИспользоватьОтборПроект
			|					ИЛИ Проект В (&МассивПроектов))) КАК Т
			|
			|СГРУППИРОВАТЬ ПО
			|	Т.Месяц,
			|	Т.Проект
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Месяц,
			|	Проект
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВТ_Факт.Период КАК Период,
			|	ВТ_Факт.Аналитика КАК Аналитика,
			|	ЕСТЬNULL(ВТ_Факт.Сумма, 0) КАК Факт,
			|	ЕСТЬNULL(ВТ_План.КоличествоЧасов, 0) КАК План
			|ИЗ
			|	ВТ_Факт КАК ВТ_Факт
			|		ПОЛНОЕ СОЕДИНЕНИЕ ВТ_План КАК ВТ_План
			|		ПО ВТ_Факт.Период = ВТ_План.Месяц
			|			И ВТ_Факт.Аналитика = ВТ_План.Проект
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ВТ_Факт
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ВТ_План";    
		
		Запрос.УстановитьПараметр("НачалоПериода", ПараметрыОтчета.ПериодОтчета.ДатаНачала);
	    Запрос.УстановитьПараметр("КонецПериода", ПараметрыОтчета.ПериодОтчета.ДатаОкончания);  
		Запрос.УстановитьПараметр("НеИспользоватьОтборПроект", НеИспользоватьОтборПроект);
		Запрос.УстановитьПараметр("МассивПроектов", МассивПроектов);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			НовыйРезультат = ТаблицаРезультата.Добавить();     
			НовыйРезультат.Показатель = Показатель;
			НовыйРезультат.ИдентификаторПоказателя = "fe066537_806c_11f0_945b_985f41b2a73b";
			НовыйРезультат.Аналитика = Выборка.Аналитика;
			НовыйРезультат.Период = Выборка.Период;
			НовыйРезультат.СуммаПлан = Выборка.План;
			НовыйРезультат.СуммаФакт = Выборка.Факт;
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры


// Процедура заполняет расшифровку показателя
//
// Параметры:
//  Показатель			 - СправочникСсылка.ПоказателиБизнеса	 - показатель по которому получаем данные
//  ПараметрыОтчета		 - Структура - параметры формирования отчета
//   * ПериодОтчета		 - СтандартныйПериод - период за который формируется отчет
//   * Периодичность	 - ПеречислениеСсылка.Периодичность - выбранная периодичность: месяц, квартал, год
//   * Сценарий			 - СправочникСсылка.СценарииПланирования - сценарий для вариантов план и план/факт
//   * Группировка		 - ПеречислениеСсылка.ГруппировкаАнализаБизнеса - вариант группировки по аналитике
//   * Отбор			 - Структура - отборы установленные в отчете
//  ТаблицаРезультата	 - ТаблицаЗначений	 - таблица для помещения результата. Колонки:
//   * Период			 - Дата - период к которому относится сумма по строке
//   * Документ		 - Произвольный - документ движения
//   * Проект			 - СправочникСсылка.Проекты - Аналитика по проекту
//   * Подразделение	 - СправочникСсылка.СтруктурныеЕдиницы - Аналитика по подразделениям
//   * Содержание		 - Строка - Содержание операции
//   * Аналитика		 - Произвольный - аналитика показателя: проекты или подразделения
//   * Сумма			 - Число - сумма показателя
//
Процедура ПолучитьРасшифровкуПоказателя_fe066537_806c_11f0_945b_985f41b2a73b(Показатель, ПараметрыОтчета, ТаблицаРезультата) Экспорт

	Запрос = Новый Запрос; 
	Если ПараметрыОтчета.ПланФакт = Перечисления.ПланФакт.Факт Тогда 
		
		Запрос.Текст = 
			"ВЫБРАТЬ         
			|	НАЧАЛОПЕРИОДА(Задание.Дата, МЕСЯЦ) КАК Период,
			|	Задание.Ссылка КАК Документ,
			|	Т.Проект КАК Проект,
			|	Задание.СтруктурнаяЕдиница КАК Подразделение,
			|	""Разработка по часам"" КАК Содержание,
			|	Т.Проект КАК Аналитика,
			|	Т.ФактическиеЗатраты КАК Сумма
			|ИЗ
			|	РегистрСведений.Пр_ПланФактовоеОтклонение КАК Т
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаданиеНаРаботу КАК Задание
			|		ПО Т.ЗаданиеНаРаботу = Задание.Ссылка
			|ГДЕ
			|	Задание.Дата МЕЖДУ &НачалоПериода И &КонецПериода";   
		
		Запрос.УстановитьПараметр("НачалоПериода", ПараметрыОтчета.ПериодОтчета.ДатаНачала);
	    Запрос.УстановитьПараметр("КонецПериода", ПараметрыОтчета.ПериодОтчета.ДатаОкончания); 
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			НовыйРезультат = ТаблицаРезультата.Добавить(); 
			НовыйРезультат.Показатель = Показатель;
			НовыйРезультат.ИдентификаторПоказателя = "fe066537_806c_11f0_945b_985f41b2a73b";
			НовыйРезультат.Период = Выборка.Период;			
			НовыйРезультат.Документ = Выборка.Документ;
			НовыйРезультат.Проект = Выборка.Проект;
			НовыйРезультат.Подразделение = Выборка.Подразделение;
			НовыйРезультат.Аналитика = Выборка.Аналитика;
			НовыйРезультат.Содержание = СтрШаблон("Разработка по часам %1", Выборка.Период);
			НовыйРезультат.Сумма = Выборка.Сумма;
		КонецЦикла;
		
	ИначеЕсли ПараметрыОтчета.ПланФакт = Перечисления.ПланФакт.План Тогда
		
	КонецЕсли;
	
КонецПроцедуры