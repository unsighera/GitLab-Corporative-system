#Если Сервер Или ВнешнееСоединение Или ТолстыйКлиентОбычноеПриложение Тогда
	
#Область ПрограммныйИнтерфейс

Функция СформироватьТаблицуСПропорциональнымРаспределением(Дата) Экспорт
	
	РазработкаПоЧасам = Пр_КорпоративнаяСистемаGitLabВстраиваниеУНФСервер.НоменклатураРазработкаПоЧасам();
	ЗарплатаПрограммистов = Пр_КорпоративнаяСистемаGitLabВстраиваниеУНФСервер.СчетРасчетаСПрограммистами(); 
	НаправлениеДеятельности = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РазработкаПоЧасам, "НаправлениеДеятельности", Истина);
	ГрафикПятидневка = Справочники.ГрафикиРаботы.НайтиПоНаименованию("Пятидневка", Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Пр_ПланФактовоеОтклонение.Проект КАК Проект,
		|	Пр_ПланФактовоеОтклонение.Разработчик КАК Разработчик,
		|	Пр_ПланФактовоеОтклонение.ФактическиеЗатраты КАК ФактическиеЗатраты,
		|	Пр_ПланФактовоеОтклонение.Контрагент КАК Контрагент
		|ПОМЕСТИТЬ ВТ_ОтработанныеЧасы
		|ИЗ
		|	РегистрСведений.Пр_ПланФактовоеОтклонение КАК Пр_ПланФактовоеОтклонение
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаданиеНаРаботу КАК ЗаданиеНаРаботу
		|		ПО Пр_ПланФактовоеОтклонение.Регистратор = ЗаданиеНаРаботу.Ссылка
		|ГДЕ
		|	ЗаданиеНаРаботу.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Проект,
		|	Разработчик,
		|	Контрагент
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТекущиеКадровыеДанныеСотрудниковУНФ.Сотрудник КАК Сотрудник,
		|	ТекущиеКадровыеДанныеСотрудниковУНФ.ТекущаяТарифнаяСтавка КАК ТекущаяТарифнаяСтавка,
		|	ТекущиеКадровыеДанныеСотрудниковУНФ.ТекущаяДолжность КАК ТекущаяДолжность
		|ПОМЕСТИТЬ ВТ_Оклады
		|ИЗ
		|	РегистрСведений.ТекущиеКадровыеДанныеСотрудниковУНФ КАК ТекущиеКадровыеДанныеСотрудниковУНФ
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВидыЦенКонтрагентов.Ссылка КАК ВидЦен
		|ПОМЕСТИТЬ ВТ_ВидыЦен
		|ИЗ
		|	Справочник.ВидыЦенКонтрагентов КАК ВидыЦенКонтрагентов
		|ГДЕ
		|	НЕ ВидыЦенКонтрагентов.ПометкаУдаления
		|	И ВидыЦенКонтрагентов.Владелец В
		|			(ВЫБРАТЬ
		|				Т.Контрагент
		|			ИЗ
		|				ВТ_ОтработанныеЧасы КАК Т)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЦеныНоменклатурыКонтрагентовСрезПоследних.ВидЦенКонтрагента КАК ВидЦен,
		|	ЦеныНоменклатурыКонтрагентовСрезПоследних.Цена КАК Цена
		|ПОМЕСТИТЬ ВТ_ЦеныДоговоров
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатурыКонтрагентов.СрезПоследних(
		|			&Дата,
		|			ВидЦенКонтрагента В
		|					(ВЫБРАТЬ
		|						Т.ВидЦен
		|					ИЗ
		|						ВТ_ВидыЦен КАК Т)
		|				И Номенклатура = &РазработкаПоЧасам) КАК ЦеныНоменклатурыКонтрагентовСрезПоследних
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ВидЦен
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_ВидыЦен
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВидыЦенКонтрагентов.Пр_Проект КАК Проект,
		|	ВТ_ЦеныДоговоров.Цена КАК Цена
		|ПОМЕСТИТЬ ВТ_КонтрагентЦена
		|ИЗ
		|	ВТ_ЦеныДоговоров КАК ВТ_ЦеныДоговоров
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЦенКонтрагентов КАК ВидыЦенКонтрагентов
		|		ПО ВТ_ЦеныДоговоров.ВидЦен = ВидыЦенКонтрагентов.Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Проект
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_ЦеныДоговоров
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ОтработанныеЧасы.Разработчик КАК Разработчик,
		|	ВТ_ОтработанныеЧасы.Проект КАК Проект,
		|	СУММА(ВТ_ОтработанныеЧасы.ФактическиеЗатраты) КАК ФактическиеЗатраты,
		|	ВТ_Оклады.ТекущаяТарифнаяСтавка КАК Оклад,
		|	ВТ_КонтрагентЦена.Цена КАК Цена,
		|	ВТ_Оклады.ТекущаяДолжность КАК Должность
		|ИЗ
		|	ВТ_ОтработанныеЧасы КАК ВТ_ОтработанныеЧасы
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Оклады КАК ВТ_Оклады
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Должности КАК Должности
		|			ПО ВТ_Оклады.ТекущаяДолжность = Должности.Ссылка
		|		ПО ВТ_ОтработанныеЧасы.Разработчик = ВТ_Оклады.Сотрудник
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_КонтрагентЦена КАК ВТ_КонтрагентЦена
		|		ПО ВТ_ОтработанныеЧасы.Проект = ВТ_КонтрагентЦена.Проект
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ОтработанныеЧасы.Разработчик,
		|	ВТ_ОтработанныеЧасы.Проект,
		|	ВТ_Оклады.ТекущаяТарифнаяСтавка,
		|	ВТ_КонтрагентЦена.Цена,
		|	ВТ_Оклады.ТекущаяДолжность
		|ИТОГИ
		|	СУММА(ФактическиеЗатраты)
		|ПО
		|	Разработчик
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_ОтработанныеЧасы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_Оклады
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_КонтрагентЦена";
	
	Запрос.УстановитьПараметр("Дата", КонецМесяца(Дата)); 
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Дата));
	Запрос.УстановитьПараметр("РазработкаПоЧасам", РазработкаПоЧасам);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаСотрудник = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ТаблицаРезультат = Новый ТаблицаЗначений;
	ТаблицаРезультат.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	ТаблицаРезультат.Колонки.Добавить("Должность", Новый ОписаниеТипов("СправочникСсылка.Должности"));
	ТаблицаРезультат.Колонки.Добавить("ВидНачисленияУдержания", Новый ОписаниеТипов("СправочникСсылка.ВидыНачисленийИУдержаний")); 
	ТаблицаРезультат.Колонки.Добавить("ДатаНачала", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ТаблицаРезультат.Колонки.Добавить("ДатаОкончания", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ТаблицаРезультат.Колонки.Добавить("ОтработаноЧасов", ОбщегоНазначения.ОписаниеТипаЧисло(10, 3));
	ТаблицаРезультат.Колонки.Добавить("Сумма", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2)); 
	ТаблицаРезультат.Колонки.Добавить("СчетЗатрат", Новый ОписаниеТипов("ПланСчетовСсылка.Управленческий")); 
	ТаблицаРезультат.Колонки.Добавить("НаправлениеДеятельности", Новый ОписаниеТипов("СправочникСсылка.НаправленияДеятельности"));
	ТаблицаРезультат.Колонки.Добавить("Проект", Новый ОписаниеТипов("СправочникСсылка.Проекты")); 
	ТаблицаРезультат.Колонки.Добавить("ГрафикРаботы", Новый ОписаниеТипов("СправочникСсылка.ГрафикиРаботы")); 
	
	Пока ВыборкаСотрудник.Следующий() Цикл
		
		Выборка = ВыборкаСотрудник.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			РаспределениеНаПроект = Выборка.Оклад * Выборка.ФактическиеЗатраты / ВыборкаСотрудник.ФактическиеЗатраты; 
			НоваяСтрока = ТаблицаРезультат.Добавить();
			НоваяСтрока.Сотрудник = Выборка.Разработчик;
			НоваяСтрока.Должность = Выборка.Должность;
			НоваяСтрока.ВидНачисленияУдержания = Справочники.ВидыНачисленийИУдержаний.СдельнаяОплатаПроцент;
			НоваяСтрока.ДатаНачала = НачалоМесяца(Дата);
			НоваяСтрока.ДатаОкончания = КонецМесяца(Дата);
			НоваяСтрока.ОтработаноЧасов = Выборка.ФактическиеЗатраты;
			НоваяСтрока.Сумма = РаспределениеНаПроект;
			НоваяСтрока.СчетЗатрат = ЗарплатаПрограммистов;
			НоваяСтрока.НаправлениеДеятельности = НаправлениеДеятельности;
			НоваяСтрока.Проект = Выборка.Проект; 
			НоваяСтрока.ГрафикРаботы = ГрафикПятидневка;
			
		КонецЦикла; 
		
	КонецЦикла;	
	
	Возврат ТаблицаРезультат;
	
КонецФункции

#КонецОбласти

#КонецЕсли