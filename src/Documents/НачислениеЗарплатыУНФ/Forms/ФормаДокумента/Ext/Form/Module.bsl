#Область ОбработчикиСобытий

&НаСервере
Процедура Пр_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	Пр_МодификацияФормСервер.ПриСозданииНаСервере(ЭтотОбъект, СтандартнаяОбработка, Отказ);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Пр_РассчитатьЗППрограммистов(Команда)
	
	ДлительнаяОперация = НачатьРасчетСуммы();

	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	Оповещение = Новый ОписаниеОповещения("ПриЗавершенииФормированияДокументов", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Оповещение, ПараметрыОжидания);

	
КонецПроцедуры

#КонецОбласти  

#Область СлужебныеПроцедурыИФункции

// Функция старта длительной операции
//
// Возвращаемое значение:
// см. ДлительныеОперации.ВыполнитьФункцию()
//
&НаСервере
Функция НачатьРасчетСуммы()

	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения,
	"Пр_НачислениеЗарплатыСервер.СформироватьТаблицуСПропорциональнымРаспределением",
	Объект.Дата);

КонецФункции

// Обработка результата выполнения фонового задания
//
// Параметры:
// Результат - см. ДлительныеОперацииКлиент.НовыйРезультатДлительнойОперации
// ДополнительныеПараметры - Неопределено
//
&НаКлиенте
Процедура ПриЗавершенииФормированияДокументов(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Неопределено Тогда 
		Возврат;
	КонецЕсли;

	Если Результат.Статус = "Ошибка" Тогда 
		
		ПоказатьПредупреждение(,Результат.ПодробноеПредставлениеОшибки,,
							   НСтр("ru = 'Ошибка при формировании документов'"));
		Возврат;
	КонецЕсли; 
	
	ЗагрузитьТаблицуРезультатСервер(Результат.АдресРезультата)

КонецПроцедуры

// Загружает в тч НачисленияУдержания данные 
//
// Параметры:
// АдресРезультата - Строка - Адрес во временном хранилище
//
&НаСервере
Процедура ЗагрузитьТаблицуРезультатСервер(Знач АдресРезультата) 
	
	ТЗРезультат = ПолучитьИзВременногоХранилища(АдресРезультата);   
	УдалитьИзВременногоХранилища(АдресРезультата);	
	Если ТЗРезультат <> Неопределено Тогда
		Объект.НачисленияУдержания.Загрузить(ТЗРезультат);
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Рассчет произведен успешно за текущий месяц'"));
		Возврат;
	КонецЕсли;
	ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Рассчет за текущий месяц не произведен. Ошибка в результате выполнения'")); 

КонецПроцедуры

#КонецОбласти