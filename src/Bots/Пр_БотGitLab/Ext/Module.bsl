#Область ОбработчикиСобытий

&После("ОбработкаСообщенияСистемыВзаимодействия")
Процедура Пр_ОбработкаСообщенияСистемыВзаимодействия(Сообщение, ДополнительныеПараметры)
	
	Пр_ОбменGitLabСервер.ПодготовитьИОтправитьКомментарийIssueGitLab(Сообщение); 
	ОбработатьСообщениеОпроса(Сообщение);
	
КонецПроцедуры  

#КонецОбласти  

#Область СлужебныеПроцедурыИфункции

// Обработка опросника
//
// Параметры:
// Сообщение - СообщениеСистемыВзаимодействия
//
Процедура ОбработатьСообщениеОпроса(Сообщение)

	ИдентификаторОбсуждения = Сообщение.Обсуждение;
	УстановитьПривилегированныйРежим(Истина);
	Обсуждение = СистемаВзаимодействия.ПолучитьОбсуждение(ИдентификаторОбсуждения); 
	УстановитьПривилегированныйРежим(Ложь);
	Если Обсуждение.КонтекстОбсуждения <> Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ИдентификаторАвтора = Сообщение.Автор; 
	УстановитьПривилегированныйРежим(Истина);
	АвторСистемыВзаимодействия = СистемаВзаимодействия.ПолучитьПользователя(ИдентификаторАвтора);  
	УстановитьПривилегированныйРежим(Ложь);
	Пользователь = Пользователи.НайтиПоИдентификатору(АвторСистемыВзаимодействия.ИдентификаторПользователяИнформационнойБазы);
	Если Пользователь = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекстСообщения = Сообщение.Текст;
	ЧастиСообщения = СтрРазделить(Строка(ТекстСообщения), Символы.ПС, Ложь);
	Если Не ЧастиСообщения.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	Цитата = ЧастиСообщения[0];
	Если СтрНайти(Цитата, ">") = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Вопрос = Прав(Цитата, СтрДлина(Цитата) - 1);
	ЧастиСообщения.Удалить(0);
	Ответ = СтрСоединить(ЧастиСообщения, Символы.ПС);
	
	МенеджерЗаписиОтвета = РегистрыСведений.Пр_ЕжедневныйОпрос.СоздатьМенеджерЗаписи();
	МенеджерЗаписиОтвета.Период = НачалоДня(ТекущаяДатаСеанса()); 
	МенеджерЗаписиОтвета.Пользователь = Пользователь;
	МенеджерЗаписиОтвета.Вопрос = Строка(Вопрос);
	МенеджерЗаписиОтвета.Ответ = Строка(Ответ); 
	МенеджерЗаписиОтвета.Записать(Истина);
	
	СообщениеСВ = СистемаВзаимодействия.СоздатьСообщение(ИдентификаторОбсуждения);
	СообщениеСВ.Автор = Пр_БотыСервер.ИдентификаторБотаМетаданныхGitLab();
	СообщениеСВ.Дата = ТекущаяДатаСеанса();
	
	Если Вопрос = НСтр("ru = 'Что ты делал сегодня?'") Тогда  
		
		СообщениеСВ.Текст = НСтр("ru = 'Что ты будешь делать завтра?'");	
		
	ИначеЕсли Вопрос = НСтр("ru = 'Что ты будешь делать завтра?'") Тогда    
		
		СообщениеСВ.Текст = НСтр("ru = 'Что тебе мешает или какие есть трудности?'"); 
		
	ИначеЕсли Вопрос = НСтр("ru = 'Что тебе мешает или какие есть трудности?'") Тогда
		
		СообщениеСВ.Текст = НСтр("ru = 'Спасибо за твой ответ! Если ответ оказался неверным можете повторно пройти опрос нажав на кнопку'");
		РядКнопок = СообщениеСВ.ПанельКнопок.РядыКнопок.Добавить();
		Кнопка = РядКнопок.Добавить(ДействиеКнопкиПанелиКнопокСообщенияСистемыВзаимодействия.ОбработатьНаКлиенте, НСтр("ru = 'Ответить еще раз'"));
		Кнопка.ИмяДействия = НСтр("ru = 'ОтветитьПовторноОпрос'");
		Кнопка.Картинка = БиблиотекаКартинок.Перечитать;
		Кнопка.ТекстСообщения = НСтр("ru = 'Что ты делал сегодня?'");
		
		Кнопка = РядКнопок.Добавить(ДействиеКнопкиПанелиКнопокСообщенияСистемыВзаимодействия.ОбработатьНаКлиенте, НСтр("ru = 'Завершить опрос'"));
		Кнопка.ИмяДействия = НСтр("ru = 'ЗавершитьОпрос'");
		Кнопка.Картинка = БиблиотекаКартинок.ВыполнитьЗадачу;
		Кнопка.ТекстСообщения = НСтр("ru = 'Спасибо за твой ответ! Увидимся завтра!'");
		
	КонецЕсли;
	
	СообщениеСВ.Записать();
	
КонецПроцедуры

#КонецОбласти
